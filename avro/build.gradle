import com.commercehub.gradle.plugin.avro.GenerateAvroJavaTask
import com.commercehub.gradle.plugin.avro.GenerateAvroProtocolTask

plugins {
  id "com.commercehub.gradle.plugin.avro-base" version "0.9.1"
}

dependencies {
  implementation libraries.core.avro
}

def generateProtocol = tasks.register("generateProtocol", GenerateAvroProtocolTask) {
  source file("src/main/avro-schema")
  include("**/*.avdl")
  outputDir = file("src/main/avro-generated")
}

def generateAvro = tasks.register("generateAvro", GenerateAvroJavaTask) {
  dependsOn generateProtocol

  source file("src/main/avro")
  source file("src/main/avro-generated")

  include("**/*.avpr")
  include("**/*.avsc")

  outputDir = file("src/main/java")

  createSetters = true
  fieldVisibility = "PRIVATE"
  outputCharacterEncoding = "UTF-8"
}

tasks.named("compileJava").configure {
  source(generateAvro)
}
