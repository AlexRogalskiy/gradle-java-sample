apply plugin: 'java'
apply plugin: 'maven-publish'

afterEvaluate {
  publishing {
    publications {
      mavenJava(MavenPublication) {
        groupId = GROUP
        artifactId = POM_ARTIFACT_ID
        version = VERSION_NAME

        artifact sourcesJar
        artifact javadocJar
        from components.java

        pom {
          name = POM_NAME
          packaging = POM_PACKAGING
          description = POM_DESCRIPTION
          url = POM_URL

          scm {
            url = POM_SCM_URL
            connection = POM_SCM_CONNECTION
            developerConnection = POM_SCM_DEV_CONNECTION
          }
          licenses {
            license {
              name = POM_LICENCE_NAME
              url = POM_LICENCE_URL
              distribution = POM_LICENCE_DIST
            }
          }
          developers {
            developer {
              id = POM_DEVELOPER_ID
              name = POM_DEVELOPER_NAME
            }
          }
        }
      }
    }
    repositories {
      maven {
        credentials {
          username "$System.env.BINTRAY_USER"
          password "$System.env.BINTRAY_API_KEY"
        }
        url = VERSION_NAME.endsWith('SNAPSHOT') ? SNAPSHOT_REPOSITORY : RELEASE_REPOSITORY
      }
    }
  }
}

task sourcesJar(type: Jar, dependsOn: classes) {
  //noinspection GroovyAccessibility //alternatively replace this with archiveClassifier.set('...')
  archiveClassifier.value = "sources"
  from sourceSets.main.allSource, 'build/generated/source/kapt/main', 'build/generated/source/kaptKotlin/main'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
  //noinspection GroovyAccessibility //alternatively replace this with archiveClassifier.set('...')
  archiveClassifier.value = "javadoc"
  from javadoc.destinationDir
}
