plugins {
  id("com.github.ben-manes.versions")
  id("com.github.johnrengelman.shadow")
  id("com.adarshr.test-logger") version "2.1.1"
  id("com.diffplug.spotless") version "5.9.0"
  id("com.gorylenko.gradle-git-properties") version "2.2.4"
  id("io.franzbecker.gradle-lombok") version "4.0.0"

  id("org.sonarqube")
  id("java")
  id("maven")
  id("checkstyle")
  id("java-gradle-plugin")
}

repositories {
  mavenCentral()
  mavenLocal()
  google()
  jcenter()

  maven {
    name "Artifactory repository"
    url "https://oss.jfrog.org/artifactory/oss-snapshot-local/"
  }
  maven {
    name "Jitpack repository"
    url 'https://jitpack.io'
  }
  maven {
    name "JCenter gradle plugins repository"
    url "https://dl.bintray.com/gradle/gradle-plugins"
  }
  maven {
    name "Sonatype snapshots repository"
    url "https://oss.sonatype.org/content/repositories/snapshots/"
  }
  maven {
    name "Gradle plugins repository"
    url "https://plugins.gradle.org/m2/"
  }
  maven {
    name "IceRockDev gradle plugins repository"
    url "https://dl.bintray.com/icerockdev/plugins"
  }
}

allprojects {
  apply plugin: 'com.github.johnrengelman.shadow'
  apply plugin: 'com.adarshr.test-logger'
  apply plugin: 'com.diffplug.spotless'
  apply plugin: 'java'
  apply plugin: 'jacoco'

  repositories {
    jcenter()
    mavenCentral()

    maven {
      name "KotlinX repository"
      url "https://kotlin.bintray.com/kotlinx"
    }
    maven {
      name "Artifactory repository"
      url "https://oss.jfrog.org/artifactory/oss-snapshot-local/"
    }
  }

//  jacocoTestReport {
//    reports {
//      html.enabled = true
//      xml.enabled = true
//      csv.enabled = false
//    }
//  }

  gitProperties {
    failOnNoGitDirectory = false
    dateFormat = "yyyy-MM-dd'T'HH:mmZ"
    dateFormatTimeZone = "PST"
  }

  def buildProperties = new Properties()
  file(rootDir.absolutePath + "/build.properties").withInputStream { buildProperties.load(it) }
  if (buildProperties.getProperty("build.debug").toBoolean()) {

    testlogger {
      theme 'mocha'
      slowThreshold 5000
    }

    spotless {
      format 'misc', {
        target '**/*.gradle', '**/*.md', '**/.gitignore'
        targetExclude 'docs/**'

        trimTrailingWhitespace()
        indentWithSpaces()
        endWithNewline()
      }
      java {
        target 'build/**/*.java'
        targetExclude '**/avro/*.java', 'flatbuffers/**/*.java', 'thrift/**/*.java', '**/protobuf/*.java'
        googleJavaFormat().aosp()
      }
      kotlin {
        target '**/*.kt'
        ktlint()
        trimTrailingWhitespace()
        endWithNewline()
      }
    }
  }
}

subprojects {
  apply from: rootProject.file('buildSrc/src/main/groovy/setup.gradle')
  apply from: rootProject.file('buildSrc/src/main/groovy/dependencies.gradle')
  apply from: rootProject.file('buildSrc/src/main/groovy/subproject.gradle')

  apply plugin: 'java'
  apply plugin: 'groovy'

  group projectConfig.group
  version projectConfig.version
  description = projectConfig.description

  sourceCompatibility = projectConfig.sourceCompatibility
  targetCompatibility = projectConfig.targetCompatibility

  archivesBaseName = project.buildFile
  project.buildDir = projectConfig.projectBuildDir

  repositories {
    mavenCentral()
    jcenter()
    google()
  }

  dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    // guava library dependencies
    implementation libraries.core.guava

    // commons library dependencies
    implementation libraries.core.commons_lang
    implementation libraries.core.commons_text

    // javapoet library dependencies
    implementation libraries.core.javapoet

    // lombok library dependencies
    implementation libraries.core.lombok
    annotationProcessor libraries.core.lombok

    // lombok test library dependencies
    testImplementation libraries.core.lombok
    testAnnotationProcessor libraries.core.lombok

    // assertj test library dependencies
    testImplementation libraries.test.assertj

    // hamcrest test library dependencies
    testImplementation libraries.test.hamcrest

    // mockito test library dependencies
    testImplementation libraries.test.mockito
    testImplementation libraries.test.mockito_junit5

    // junit test library dependencies
    testImplementation libraries.test.junit5_api
    testImplementation libraries.test.junit5_engine
    testImplementation libraries.test.junit5_params
    testImplementation libraries.test.junit5_runner
    testImplementation(libraries.test.junit5_launcher) {
      excludeJunit
    }
  }

  dependencyUpdates.resolutionStrategy {
    componentSelection { rules ->
      rules.all { ComponentSelection selection ->
        boolean rejected = ["alpha", "beta", "rc", "cr", "m", "preview", "b", "ea", "snapshot"].any {
          qualifier ->
            selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-+]*/
        }
        if (rejected) {
          selection.reject("Release candidate")
        }
      }
    }
  }

  jar {
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    //from { configurations.runtimeClasspath.get().filter { it.name.endsWith("jar") }.map { zipTree(it) } }

    manifest {
      attributes("Automatic-Module-Name": projectConfig.module)
      attributes("Build-Date": Instant.now().toString())
      attributes("Class-Path": configurations.compile.collect { it.getPath() }.join(' '))
    }
  }
}

task stage(dependsOn: ['clean'])

task copyToLib(type: Copy) {
  from "$buildDir/libs"
  into "$rootProject.buildDir/libs"
}
copyToLib.dependsOn(stage)
stage.dependsOn(copyToLib)

task copyLicense {
  outputs.file new File("$buildDir/LICENSE")
  doLast {
    copy {
      from "LICENSE"
      into "$buildDir"
    }
  }
}
